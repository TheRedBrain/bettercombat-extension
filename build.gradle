plugins {
	id 'fabric-loom' version '1.5-SNAPSHOT'
	id "com.matthewprenger.cursegradle" version "1.4.0"
	id "com.github.breadmoirai.github-release" version "2.4.1"
	id "com.modrinth.minotaur" version "2.+"
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {

	// Cloth Config
	maven {
		url "https://maven.shedaniel.me/"
	}

	// Cloth Config
	maven {
		name = "TerraformersMC"
		url = "https://maven.terraformersmc.com/"
	}

	// Better Combat, Stamina Attributes
	maven {
		name = "Modrinth"
		url = "https://api.modrinth.com/maven"
		content {
			includeGroup "maven.modrinth"
		}
	}

	// Player Animator
	maven {
		name "KosmX's maven"
		url 'https://maven.kosmx.dev/'
	}

	// Mixin Squared
	maven {
		url 'https://jitpack.io'
	}

	// Shoulder Surfing Reloaded
	maven {
		url "https://www.cursemaven.com"
		content {
			includeGroup("curse.maven")
		}
	}

}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// Cloth Config
	modImplementation ("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config}")

	// Player Animation Lib
	modImplementation("dev.kosmx.player-anim:player-animation-lib-fabric:${project.player_anim}")

	// Better Combat
	modImplementation "maven.modrinth:better-combat:${project.better_combat}"

	// Shoulder Surfing Reloaded
	modImplementation("curse.maven:shoulder-surfing-reloaded-243190:${project.shoulder_surfing_file_id}")

	// Stamina Attributes
	modImplementation "maven.modrinth:stamina-attributes:${project.stamina_attributes}"

	// Mixin Squared
	include(implementation(annotationProcessor("com.github.bawnorton.mixinsquared:mixinsquared-fabric:0.1.2-beta.4")))

}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

ext.changelog_last_section = { ->
	return new File("CHANGELOG.md").text.split('\n# ')[0]
}

if(project.hasProperty('release_type')) {
	tasks.register("publishMod") {
		dependsOn(build)
		finalizedBy(tasks.curseforge)
		finalizedBy(tasks.modrinth)
		finalizedBy(tasks.githubRelease)
	}

	curseforge {
		apiKey = System.getenv("CURSEFORGE_API_KEY")
		project {
			id = System.getenv("CURSEFORGE_PROJECT_ID")
			changelog = changelog_last_section()
			changelogType = 'markdown'
			releaseType = release_type
			addGameVersion "${project.minecraft_version}"
			addGameVersion "Fabric"

			relations {
				requiredDependency 'fabric-api'
				requiredDependency 'cloth-config'
				requiredDependency 'better-combat-by-daedelus'
				optionalDependency 'shoulder-surfing-reloaded'
				optionalDependency 'stamina-attributes'
			}

			mainArtifact(remapJar)
			afterEvaluate {
				uploadTask.dependsOn(remapJar)
			}
			options {
				forgeGradleIntegration = false
			}
		}
	}

	modrinth {
		token = System.getenv("MODRINTH_API_KEY")
		projectId = System.getenv("MODRINTH_PROJECT_ID") // This can be the project ID or the slug. Either will work!
		versionName = "${version}" // You don't need to set this manually. Will fail if Modrinth has this version already
		versionNumber = "${version}" // You don't need to set this manually. Will fail if Modrinth has this version already
		versionType = release_type // This is the default -- can also be `beta` or `alpha`
		uploadFile = remapJar // With Loom, this MUST be set to `remapJar` instead of `jar`!
		gameVersions = ["${project.minecraft_version}"] // Must be an array, even with only one version
		loaders = ["fabric"] // Must also be an array - no need to specify this if you're using Loom or ForgeGradle
		changelog = changelog_last_section()
		dependencies { // A special DSL for creating dependencies
			// scope.type
			// The scope can be `required`, `optional`, `incompatible`, or `embedded`
			// The type can either be `project` or `version`
			required.project 'fabric-api'
			required.project 'cloth-config'
			required.project 'better-combat'
			optional.project 'shoulder-surfing-reloaded'
			optional.project 'stamina-attributes'
		}
	}

	githubRelease {
		token System.getenv("GH_API_KEY") // This is your personal access token with Repo permissions
		owner System.getenv("GH_OWNER") // default is the last part of your group. Eg group: "com.github.breadmoirai" => owner: "breadmoirai"
		repo System.getenv("GH_REPO") // by default this is set to your project name
		tagName "${version}" // by default this is set to "v${project.version}"
		targetCommitish "${project.minecraft_version}" // by default this is set to "main"
		releaseName "Release" // Release title, by default this is the same as the tagName
		generateReleaseNotes false // Generate release notes automatically, if true and body is present, body will be pre-pended, if name is not given, one will be generated by the tag
		// body.set "" // by default this is empty
		draft false // by default this is true
		prerelease release_type != "release" // by default this is false
		// releaseAssets jar.destinationDir.listFiles // this points to which files you want to upload as assets with your release, by default this is empty
		allowUploadToExisting false // Setting this to true will allow this plugin to upload artifacts to a release if it found an existing one. If overwrite is set to true, this option is ignored.
		overwrite false // by default false; if set to true, will delete an existing release with the same tag and name
		// dryRun true // by default false; you can use this to see what actions would be taken without making a release
		apiEndpoint "https://api.github.com" // should only change for github enterprise users
		client // This is the okhttp client used for http requests
	}
}
